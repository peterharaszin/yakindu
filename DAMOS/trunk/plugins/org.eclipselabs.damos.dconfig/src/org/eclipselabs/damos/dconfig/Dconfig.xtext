grammar org.eclipselabs.damos.dconfig.Dconfig with org.eclipselabs.damos.mscript.Mscript

import "platform:/resource/org.eclipselabs.damos.dml/model/DML.ecore"
import "platform:/resource/org.eclipselabs.damos.dconfig/model/Dconfig.ecore"
import "platform:/resource/org.eclipselabs.damos.mscript/model/ComputationModel.ecore"
import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

Configuration:
	'package' packageName=QualifiedName
	(importDeclarations+=ImportDeclaration)*
	'configuration' name=ValidID ('extends' baseConfiguration=[Configuration|QualifiedName])? '{'
		(properties+=DeclaredProperty)*
		(rootSystemConfiguration=RootSystemConfiguration)?
	'}';
	
Property:
	DeclaredProperty | ComputationProperty;
	
DeclaredProperty:
	SimpleProperty | SelectionProperty;
	
SimpleProperty:
	(propagate?='propagate')? (declaration=[SimplePropertyDeclaration|QualifiedNameWithoutIJ] '=' value=Expression | 'unset' declaration=[SimplePropertyDeclaration|QualifiedName]);

SelectionProperty:
	(propagate?='propagate')? 'select' declaration=[SelectionPropertyDeclaration|QualifiedName] selection=[SelectionPropertyOption|QualifiedName] ('as' name=ValidID)? (body=SelectionPropertyBody)?;
	
SelectionPropertyBody:
	{SelectionPropertyBody} '{'
		(properties+=DeclaredProperty)*
	'}';
	
RootSystemConfiguration:
	'system' contextFragment=[Fragment|QualifiedName] (body=SystemConfigurationBody)?;

SubsystemConfiguration:
	'subsystem' subsystem=[Subsystem|ValidID] body=SystemConfigurationBody;

SystemConfigurationBody:
	{SystemConfigurationBody} '{'
		(properties+=Property |
		bindings+=Binding |
		componentConfigurations+=ComponentConfiguration |
		fragmentConfigurations+=FragmentConfiguration |
		subsystemConfigurations+=SubsystemConfiguration)*
	'}';

FragmentConfiguration:
	'fragment' (startFragment=[Fragment|QualifiedName] (range?='..' (endFragment=[Fragment|QualifiedName])?)? | range?='..' endFragment=[Fragment|QualifiedName]) body=FragmentConfigurationBody; 

FragmentConfigurationBody:
	{FragmentConfigurationBody} '{'
		(properties+=Property)*
	'}';

ComponentConfiguration:
	'component' component=[Component|ValidID] body=ComponentConfigurationBody;

ComponentConfigurationBody:
	{ComponentConfigurationBody} '{'
		(properties+=Property)*
	'}';

ComputationProperty:
	(propagate?='propagate')? 'computation' '{'
		computationModel=ComputationModel
	'}';

Binding:
	'bind' targetPath=BindingTargetPath 'to' source=[Component|ValidID]
		(body=BindingBody)?;

BindingTargetPath:
	propertyReferences+=BindingPropertyReference ('.' propertyReferences+=BindingPropertyReference)* '.' resource=[ResourceDeclaration|ValidID] ('[' subscript=BindingSubscript ']')?;

BindingPropertyReference:
	property=[SelectionProperty|ValidID];

BindingBody:
	{BindingBody} '{' (properties+=DeclaredProperty)* '}';

BindingSubscript:
	index=ValidInt;

ComputationModel:
	{ComputationModel} (numberFormatMappings+=NumberFormatMapping)*;

NumberFormat:
	FloatingPointFormat | FixedPointFormat;

FloatingPointFormat:
	kind=PredefinedFloatingPointFormatKind;

enum PredefinedFloatingPointFormatKind returns FloatingPointFormatKind:
	Binary32='float32' |
	Binary64='float64';
	
enum PredefinedFixedPointFormatKind:
	Int8='int8' |
	Int16='int16' |
	Int32='int32' |
	Int64='int64' |
	Int128='int128' |
	UInt8='uint8' |
	UInt16='uint16' |
	UInt32='uint32' |
	UInt64='uint64' |
	UInt128='uint128' |
	Fract8='fract8' |
	Fract16='fract16' |
	Fract32='fract32' |
	Fract64='fract64' |
	Fract128='fract128';

FixedPointFormat:
	((('fix' | unsigned?='ufix') integerLength=ValidInt (('.' fractionLength=ValidInt) | (('slope' slope=ValidDouble)? ('bias' bias=ValidDouble)?))) | (predefinedKind=PredefinedFixedPointFormatKind)) (saturate?='saturate')?;

NumberFormatMapping:
	'map' typeSpecifier=DataTypeSpecifier 'to' numberFormat=NumberFormat;

QualifiedNameWithWildcard:
	QualifiedName ('.*' | '.' '*')?;

QualifiedNameWithoutIJ:
	ValidIDWithoutIJ ('.' ValidIDWithoutIJ)*;

ValidIDWithoutIJ:
	N | E | ID;

ValidDouble returns ecore::EDouble hidden():
	('-')? ValidInt (('.' ValidInt (EXP | (E ('+' | '-') ValidInt))?) | (EXP | (E ('+' | '-') ValidInt)))?;
