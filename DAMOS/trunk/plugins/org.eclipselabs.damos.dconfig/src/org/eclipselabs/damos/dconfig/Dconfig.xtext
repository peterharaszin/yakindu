grammar org.eclipselabs.damos.dconfig.Dconfig with org.eclipselabs.damos.mscript.Mscript

import "platform:/resource/org.eclipselabs.damos.dml/model/DML.ecore"
import "platform:/resource/org.eclipselabs.damos.dconfig/model/Dconfig.ecore"
import "platform:/resource/org.eclipselabs.damos.mscript/model/ComputationModel.ecore"
import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

Configuration:
	'package' packageName=QualifiedName
	(importDeclarations+=ImportDeclaration)*
	'configuration' name=ValidID ('extends' baseConfiguration=[Configuration|QualifiedName])? '{'
		(runnerSpecifier=RunnerSpecifier)?
		((properties+=DeclaredProperty)+ (rootSystemConfiguration=RootSystemConfiguration)? |
		rootSystemConfiguration=RootSystemConfiguration (properties+=DeclaredProperty)*)?
	'}';
	
RunnerSpecifier:
	(auto?='auto')? 'run' runnerDeclaration=[RunnerDeclaration|QualifiedName];

Property:
	DeclaredProperty | ComputationProperty;
	
DeclaredProperty:
	SimpleProperty | SelectionProperty;
	
SimpleProperty:
	(propagate?='propagate')? (declaration=[SimplePropertyDeclaration|QualifiedNameWithoutIJ] '=' value=Expression | 'unset' declaration=[SimplePropertyDeclaration|QualifiedName]);

SelectionProperty:
	(propagate?='propagate')? 'select' declaration=[SelectionPropertyDeclaration|QualifiedName] selection=[SelectionPropertyOption|QualifiedName] ('as' name=ValidID)? (body=SelectionPropertyBody)?;
	
SelectionPropertyBody:
	{SelectionPropertyBody} '{'
		(properties+=DeclaredProperty)*
	'}';
	
RootSystemConfiguration:
	'system' contextFragment=[Fragment|QualifiedName] (body=SystemConfigurationBody)?;

SubsystemConfiguration:
	'subsystem' subsystem=[Subsystem|ValidID] body=SystemConfigurationBody;

SystemConfigurationBody:
	{SystemConfigurationBody} '{'
		(properties+=Property |
		mappings+=Mapping |
		componentConfigurations+=ComponentConfiguration |
		fragmentConfigurations+=FragmentConfiguration |
		subsystemConfigurations+=SubsystemConfiguration)*
	'}';

FragmentConfiguration:
	'fragment' (startFragment=[Fragment|QualifiedName] (range?='..' (endFragment=[Fragment|QualifiedName])?)? | range?='..' endFragment=[Fragment|QualifiedName]) body=FragmentConfigurationBody; 

FragmentConfigurationBody:
	{FragmentConfigurationBody} '{'
		(properties+=Property)*
	'}';

ComponentConfiguration:
	'component' component=[Component|ValidID] body=ComponentConfigurationBody;

ComponentConfigurationBody:
	{ComponentConfigurationBody} '{'
		(properties+=Property)*
	'}';

ComputationProperty:
	(propagate?='propagate')? 'computation' computationModel=[ComputationModel|QualifiedName];
	
Mapping:
	'map' source=[Component|ValidID] 'to' targetProperty=[SelectionProperty|ValidID]
		'.' targetResource=[ResourceDeclaration|ValidID] ('[' subscript=MappingSubscript ']')?
		(body=MappingBody)?;

MappingBody:
	{MappingBody} '{' (properties+=DeclaredProperty)* '}';

MappingSubscript:
	index=ValidInt;

QualifiedNameWithWildcard:
	QualifiedName ('.*' | '.' '*')?;

QualifiedNameWithoutIJ:
	ValidIDWithoutIJ ('.' ValidIDWithoutIJ)*;

ValidIDWithoutIJ:
	N | E | ID;
