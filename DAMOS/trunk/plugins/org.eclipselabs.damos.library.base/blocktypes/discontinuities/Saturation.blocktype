package damos.library.base.discontinuities

blockType Saturation {

	input u
	output y
	
	parameter upperLimit = 1
	parameter lowerLimit = -1

	function main(const upperLimit, const lowerLimit, u) -> y {
		check(real, <1>, <-1>) -> real
		
		static assert u is real(?) :
			error "Input value must be numeric"
			
		static assert upperLimit is real(?) :
			error "Upper limit value must be numeric"
			
		static assert lowerLimit is real(?) :
			error "Lower limit value must be numeric"
			
		static assert u is real(?) && upperLimit is real(?) => unit(u) == unit(upperLimit) :
			error "Input value and upper limit value must have same unit"
			
		static assert u is real(?) && lowerLimit is real(?) => unit(u) == unit(lowerLimit) :
			error "Input value and lower limit value must have same unit"
		
		static assert upperLimit is real(?) && lowerLimit is real(?) && unit(upperLimit) == unit(lowerLimit) => upperLimit >= lowerLimit :
			warning "Upper limit should be greater than or equal to lower limit"
		
		eq y =
			if u > upperLimit then
				upperLimit
			else if u < lowerLimit then
				lowerLimit
			else
				u
	}
	
}