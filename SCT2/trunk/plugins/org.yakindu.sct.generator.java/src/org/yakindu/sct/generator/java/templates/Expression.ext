/*
  Copyright (c) 2011 committers of YAKINDU and others.
  All rights reserved. This program and the accompanying materials
  are made available under the terms of the Eclipse Public License v1.0
  which accompanies this distribution, and is available at
  http://www.eclipse.org/legal/epl-v10.html
  Contributors:
  	Joern Seger - Initial contribution and API
  	Markus Muehlbrandt - Java adaptions
 */
import ecore;
import stext;
import sexec;
import sgraph;

extension org::yakindu::sct::generator::java::templates::Naming;
extension org::yakindu::sct::generator::java::templates::TypeModelExtensions;
extension org::yakindu::sct::generator::core::extensions::TypeAnalyzerExtensions;

String toCode(Void void) :
	"/*toCode() called with NULL element. Polymorphic resolver could not match callable method!*/";
String toCode(Void o, String statechartReference) : 
	""; //polymorphic placeholder (abstract rule)

//polymorphic placeholder (abstract rule)
String toCode(Expression expression) : 
	"/* Unknown expression: "+expression+" */"; 

String toCode(OperationDefinition this) :
	getContext() + "operationCallback." + identifier();

String toCode(ElementReferenceExpression ref) :
	if (ref.operationCall) then
		 ref.reference.toCode() + "("+ ref.args.toCode().toString(", ") + ")"
	else 
		ref.reference.toCode();

String toCode(EObject ref) :
	"/* toCode not defined for: "+ref+" */";
	
String toCode(types::Feature ref) :
	"/* toCode not defined for: "+ref+" */";
	
String toCode(FeatureCall ref) :
	ref.feature.toCode();

//polymorphic placeholder (abstract rule) 
String toCode(Statement statement) : 
	"/* Unknown statement: "+statement+" */";

//polymorphic placeholder (abstract rule)
String toCode(Literal literal) : 
	"/* Unknown literal: "+literal+" */";

String toCode(PrimitiveValueExpression primValue) :
	primValue.value.toCode();
	
String toCode(ParenthesizedExpression e) :
	"(" + e.expression.toCode() + ")";
	
String getVariableIdentifier(AssignmentExpression this) :
	getContext(varRef).toFirstLower() + varRef.getVarName().toFirstLower();

/* Assignment */
String toCode(AssignmentExpression this) :
	switch (operator) {
		case AssignmentOperator::assign :"(" + getVariableIdentifier() + " = " + expression.toCode() + ")"
		case AssignmentOperator::multAssign : "(" + getVariableIdentifier() + " *= " + expression.toCode() + ")"
		case AssignmentOperator::divAssign : "(" + getVariableIdentifier() + " /= " + expression.toCode() + ")"
		case AssignmentOperator::modAssign : "(" + getVariableIdentifier() + " %= " + expression.toCode() + ")"
		case AssignmentOperator::addAssign : "(" + getVariableIdentifier() + " += " + expression.toCode() + ")"
		case AssignmentOperator::subAssign : "(" + getVariableIdentifier() + " -= " + expression.toCode() + ")"
		case AssignmentOperator::andAssign : "(" + getVariableIdentifier() + " &= " + expression.toCode() + ")"
		case AssignmentOperator::xorAssign : "(" + getVariableIdentifier() + " ^= " + expression.toCode() + ")"
		case AssignmentOperator::orAssign : "(" + getVariableIdentifier() + " != " + expression.toCode() + ")"
		case AssignmentOperator::leftShiftAssign : "(" + getVariableIdentifier() + " <<= "+ expression.toCode() + ")"
		case AssignmentOperator::rightShiftAssign : "(" + getVariableIdentifier() + " >>= " + expression.toCode() + ")"
		default : ""
};
		
/* Literals */
String toCode(BoolLiteral expression) :
	expression.value.toString();
	
String toCode(IntLiteral expression) :
	expression.value.toString();

String toCode(HexLiteral expression) :
	expression.value.toString();

String toCode(RealLiteral expression) :
	expression.value.toString();
	
String toCode(StringLiteral expression) :
	"\"" + expression.value.toString() + "\"";

String toCode(EventValueReferenceExpression expression):
	expression.value.getContext().toFirstLower() + expression.value.getEventValueIdentifier();

/* Logical Expressions */
String toCode(LogicalOrExpression expression) :
  	"(" + expression.leftOperand.toCode() + " || " + expression.rightOperand.toCode() + ")";
  	
String toCode(LogicalAndExpression expression) :
  	"("+expression.leftOperand.toCode() + " && " + expression.rightOperand.toCode() + ")";

String toCode(LogicalNotExpression expression) :
  	" !" + expression.operand.toCode();

String toCode(LogicalRelationExpression expression) :
	if (expression.leftOperand.inferType().isString()) then
		expression.logicalString()
	else
		"("+expression.leftOperand.toCode() + getOperator(expression.operator) + expression.rightOperand.toCode()+")";

String logicalString(LogicalRelationExpression expression) : 
if(expression.operator == RelationalOperator::equals) then
	"("+expression.leftOperand.toCode()+"==null?"+expression.rightOperand.toCode()+"==null:"+expression.leftOperand.toCode()+".equals("+expression.rightOperand.toCode()+"))"
else if(expression.operator == RelationalOperator::notEquals) then
	"("+expression.leftOperand.toCode()+"==null?"+expression.rightOperand.toCode()+"==null:!"+expression.leftOperand.toCode()+".equals("+expression.rightOperand.toCode()+"))"
;


String toCode(BitwiseAndExpression expression) :
  	"("+expression.leftOperand.toCode() + " & " + expression.rightOperand.toCode()+")";

String toCode(BitwiseOrExpression expression) :
  	"("+expression.leftOperand.toCode() + " | " + expression.rightOperand.toCode()+")";

String toCode(BitwiseXorExpression expression) :
  	"("+expression.leftOperand.toCode() + " ^ " + expression.rightOperand.toCode()+")";

String toCode(ShiftExpression expression) :
  	"("+expression.leftOperand.toCode() + getOperator(expression.operator) + expression.rightOperand.toCode()+")";
  	

String toCode(NumericalAddSubtractExpression expression) :
  	expression.leftOperand.toCode() + getOperator(expression.operator) + expression.rightOperand.toCode();
  	
String toCode(NumericalMultiplyDivideExpression expression) :
  	expression.leftOperand.toCode() + getOperator(expression.operator) + expression.rightOperand.toCode();

String toCode(NumericalUnaryExpression expression) :
    getOperator(expression.operator) + " ( " + expression.operand.toCode() + " ) ";

String toCode(TimeEvent exp) :
	"timeEvents["+getEventIdentifier(exp)+".getIndex()]";
	
String toCode(VariableDefinition exp) :
	getContext(exp) + exp.getVariableIdentifier();
	
String toCode(EventDefinition exp) :
	exp.getContext()+exp.getEventIdentifier();

String toCode(ActiveStateReferenceExpression this):
	"isStateActive(State."+this.value.getName()+")";

String getOperator(AdditiveOperator operator) : 
    switch(operator) {
    	case AdditiveOperator::plus : " + "
    	case AdditiveOperator::minus : " - "
    	default : ""
    };

String getOperator(ShiftOperator operator) :
	switch(operator){
		case ShiftOperator::left : " << "
		case ShiftOperator::right : " >> "
		default : ""
	};

String getOperator(UnaryOperator operator) :
	switch (operator) {
	  case (UnaryOperator::negative) : " -"
	  case (UnaryOperator::positive) : " + "
	  case (UnaryOperator::complement) : " ~ "
	  default : ""	};

String getOperator(MultiplicativeOperator operator) : 
    switch(operator) {
    	case MultiplicativeOperator::mul : " * "
    	case MultiplicativeOperator::div : " / "
    	case MultiplicativeOperator::mod : " % "
    	default : ""
    };

String getOperator(RelationalOperator operator) :
	switch (operator) {
	  case RelationalOperator::greater : " > "
	  case RelationalOperator::greaterEqual : " >= "
	  case RelationalOperator::smaller : " < "
	  case RelationalOperator::smallerEqual : " <= "
	  case RelationalOperator::equals : " == "
	  case RelationalOperator::notEquals : " != "
	  default : ""
	};
	
String toCode(EventRaisingExpression this) :
	if this.value != null then
		event.getContext().toFirstLower()+"raise"+event.getEventName()+"("+value.toCode()+")"
	else
		event.getContext().toFirstLower()+"raise"+event.getEventName()+"()";
		
String getInitialValue(EventDefinition this) :
	//if derivation != null then
	//	derivation.condition.toCode()
	//else
	//if no initial value expression is declared set default value to avoid npe exceptions
	type.getInitialValue();
